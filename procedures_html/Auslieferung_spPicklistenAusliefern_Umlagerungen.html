<!DOCTYPE html>
<html lang="en">
<head>
    <meta charset="UTF-8">
    <meta name="viewport" content="width=device-width, initial-scale=1.0">
    <title>Auslieferung.spPicklistenAusliefern_Umlagerungen</title>
    <style>
        body { font-family: Arial, sans-serif; line-height: 1.6; margin: 0; padding: 20px; }
        h1 { color: #333; }
        pre { background-color: #f4f4f4; padding: 10px; border-radius: 5px; overflow-x: auto; }
    </style>
</head>
<body>
    <h1>Auslieferung.spPicklistenAusliefern_Umlagerungen</h1>
    <p><strong>Description:</strong> Keine Beschreibung hinterlegt.</p>
    <p><strong>Long Description:</strong> Keine ausf√ºhrliche Beschreibung hinterlegt.</p>
    <p><strong>Created At:</strong> 2024-01-12 03:32:03</p>
    <p><strong>Updated At:</strong> 2024-01-12 03:32:03</p>
    <h2>Code:</h2>
    <pre><code>CREATE PROCEDURE [Auslieferung].[spPicklistenAusliefern_Umlagerungen]

-- M&#246;gliche Fehler:
-- Diese SP ist fehlerfrei
--
-- Transaction-Handling: siehe https://msdn.microsoft.com/en-us/library/ms188378.aspx
--
@kBenutzer AS INT,
@kSessionId AS INT

AS  
SET NOCOUNT ON;
SET ANSI_NULLS ON;
SET ANSI_NULL_DFLT_ON ON;
SET ANSI_PADDING ON;
SET CONCAT_NULL_YIELDS_NULL ON;
SET XACT_ABORT OFF;

DECLARE @TranCounter INT;
SET @TranCounter = @@TRANCOUNT;
IF @TranCounter &gt; 0
    SAVE TRANSACTION spPA_Umlagerungen
ELSE
    BEGIN TRANSACTION;

BEGIN TRY
    DECLARE @OldContextInfo VARBINARY(128)
    SET @OldContextInfo = ISNULL(CONTEXT_INFO(), 0x0000);
    SET CONTEXT_INFO 0x0000;

    DECLARE @xXml XML;
    DECLARE @kWLSessionId INT;

    IF EXISTS (SELECT * FROM  dbo.tUmlagerung 
			   JOIN dbo.tLieferschein ON dbo.tLieferschein.kBestellung = dbo.tUmlagerung.kBestellung
	           WHERE dbo.tLieferschein.kSessionId = @kSessionId 
		       AND dbo.tUmlagerung.nStatus IN (1,2)) -- nStatus 1: Angelegt,  nStatus 2: Unterwegs (teillieferung)
    BEGIN
	   EXEC dbo.[spGetAndUpdatePK]
	   	  @cName = &#39;tWarenLagerEingangSessionID&#39;,
	   	  @kId = @kWLSessionId OUTPUT;

	   SET @xXml = (
	      SELECT 
	         vAuftragPosition.kArtikel AS kArtikel,
	         Streckenlager.kWarenLagerPlatz AS kWarenlagerPlatz,
	         0 AS kLieferantenBestellungPos,
	         @kBenutzer AS kBenutzer,
	         SUM(dbo.tWarenLagerAusgang.fAnzahl) AS fAnzahl, 
	         tWarenLagerEingang.fEKEinzel AS fEKEinzel,
	         &#39;&#39; AS cLieferscheinNr,
	         dbo.tWarenLagerEingang.cChargenNr AS cChargenNr,
	         dbo.tWarenLagerEingang.dMHD,
	         NULL AS dGeliefertAm,
	         &#39;Einbuchen ins Streckenlager&#39; AS cKommentar,
	         0 AS kGutschriftPos,
	         0 AS kLHM,
	         @kWLSessionId AS kSessionId,
	         vAuftragPosition.kAuftragPosition AS kBestellPosUmlagerung,
	         0 AS kRMRetourePos,
	   	    10 AS kBuchungsart
	      FROM
	         dbo.tUmlagerung     	      
	      JOIN dbo.tLieferschein ON dbo.tLieferschein.kBestellung = dbo.tUmlagerung.kBestellung
	      JOIN dbo.tLieferscheinPos ON dbo.tLieferscheinPos.kLieferschein = dbo.tLieferschein.kLieferschein
		 JOIN Verkauf.vAuftragPosition ON vAuftragPosition.kAuftragPosition = dbo.tLieferscheinPos.kBestellPos
	      JOIN dbo.tWarenLagerAusgang ON dbo.tLieferscheinPos.kLieferscheinPos = dbo.tWarenLagerAusgang.kLieferscheinPos
		 JOIN dbo.tWarenLagerEingang ON dbo.tWarenLagerEingang.kWarenLagerEingang = dbo.tWarenLagerAusgang.kWarenLagerEingang
		 JOIN (
			 SELECT 
			    dbo.tWarenLagerPlatz.kWarenLagerPlatz,
			    dbo.tWarenLager.kQuellLager,
			    dbo.tWarenLager.kZielLager
			 FROM dbo.tWarenLagerPlatz 
			 JOIN dbo.tWarenLager ON dbo.tWarenLager.kWarenLager = dbo.tWarenLagerPlatz.kWarenLager 
			 WHERE 
			    dbo.tWarenLagerPlatz.kWarenLagerPlatzTyp = 0 
			    AND dbo.tWarenLager.kQuellLager &gt; 0
			    AND dbo.tWarenLager.kZielLager &gt; 0
			    AND dbo.tWarenLager.nAktiv &gt; 0
		 ) AS Streckenlager ON Streckenlager.kQuellLager = dbo.tUmlagerung.kQuellLager AND Streckenlager.kZielLager = dbo.tUmlagerung.kZielLager
	      WHERE	  
	         dbo.tLieferschein.kSessionId = @kSessionId
	         AND dbo.tUmlagerung.nStatus IN (1,2) -- nStatus 1: Angelegt
	      GROUP BY 
		    vAuftragPosition.kArtikel,
		   -- dbo.tlieferscheinPos.kLieferscheinPos,
		    vAuftragPosition.kAuftragPosition,
		    Streckenlager.kWarenLagerPlatz,
		    dbo.tWarenLagerEingang.cChargenNr,
		    dbo.tWarenLagerEingang.dMHD,
		    dbo.tWarenLagerEingang.fEKEinzel
	      FOR XML PATH(&#39;WarenEingang&#39;), TYPE);

	   IF (@xXml IS NOT NULL)
	   BEGIN
	       EXEC dbo.[spWarenlagerEingangSchreiben] 
	   		  @xWarenlagerEingaenge = @xXml;
	   END;


	    -- Lieferantenbestellung in Bearbeitung setzen	   
		DECLARE @kLieferantenbestellung INT;
		DECLARE cLieferantenbestellung CURSOR LOCAL FAST_FORWARD FOR
		SELECT 
			dbo.tLieferantenBestellung.kLieferantenBestellung
		FROM
			dbo.tUmlagerung
		JOIN dbo.tLieferschein ON dbo.tLieferschein.kBestellung = dbo.tUmlagerung.kBestellung
		JOIN dbo.tLieferantenBestellung ON dbo.tLieferantenBestellung.kLieferantenBestellung = dbo.tUmlagerung.kLieferantenBestellung
		WHERE	  
	   		dbo.tLieferschein.kSessionId = @kSessionId 
	   		AND dbo.tUmlagerung.nStatus = 1 -- nStatus 1: Angelegt
			AND dbo.tLieferantenBestellung.nStatus &lt; 20 -- noch nicht in Bearbeitung
		OPEN cLieferantenbestellung
		FETCH NEXT FROM cLieferantenbestellung INTO @kLieferantenbestellung
		WHILE (@@FETCH_STATUS = 0)
		BEGIN
			EXEC Lieferantenbestellung.spLieferantenBestellungStatusAendern
				@kLieferantenBestellung = @kLieferantenbestellung,
				@nStatus = 20; -- 20: in Bearbeitung

			FETCH NEXT FROM cLieferantenbestellung INTO @kLieferantenbestellung
		END;

		-- Umlagerung Status hochsetzen
	    UPDATE dbo.tUmlagerung
	    SET dbo.tUmlagerung.nStatus = 2 -- nStatus 2: Unterwegs	   
	    FROM dbo.tUmlagerung 
	    JOIN dbo.tLieferschein ON dbo.tLieferschein.kBestellung = dbo.tUmlagerung.kBestellung
	    WHERE dbo.tLieferschein.kSessionId = @kSessionId 
	   	AND dbo.tUmlagerung.nStatus = 1; -- nStatus 1: Angelegt	   
	  
    END;

    IF @TranCounter = 0 
	   COMMIT TRANSACTION;
END TRY
BEGIN CATCH
    IF @TranCounter = 0
	BEGIN
		IF @@TRANCOUNT &gt; 0 ROLLBACK TRANSACTION;
	END
    ELSE
	BEGIN
		IF XACT_STATE() &lt;&gt; -1
		BEGIN
			IF @@TRANCOUNT &gt; 0 ROLLBACK TRANSACTION spPA_Umlagerungen;
		END
	END

    SET CONTEXT_INFO @OldContextInfo;

    DECLARE @ErrorMessage NVARCHAR(MAX);
    DECLARE @ErrorSeverity INT;
    DECLARE @ErrorState INT;

    SELECT @ErrorMessage = ERROR_MESSAGE();
    SELECT @ErrorSeverity = ERROR_SEVERITY();
    SELECT @ErrorState = ERROR_STATE();

    RAISERROR (@ErrorMessage, -- Message text.
               @ErrorSeverity, -- Severity.
               @ErrorState -- State.
              );
END CATCH</code></pre>
</body>
</html>
