<!DOCTYPE html>
<html lang="en">
<head>
    <meta charset="UTF-8">
    <meta name="viewport" content="width=device-width, initial-scale=1.0">
    <title>Verkauf.spAuftragStorno</title>
    <style>
        body { font-family: Arial, sans-serif; line-height: 1.6; margin: 0; padding: 20px; }
        h1 { color: #333; }
        pre { background-color: #f4f4f4; padding: 10px; border-radius: 5px; overflow-x: auto; }
    </style>
</head>
<body>
    <h1>Verkauf.spAuftragStorno</h1>
    <p><strong>Description:</strong> Keine Beschreibung hinterlegt.</p>
    <p><strong>Long Description:</strong> Keine ausf√ºhrliche Beschreibung hinterlegt.</p>
    <p><strong>Created At:</strong> 2024-01-12 03:32:04</p>
    <p><strong>Updated At:</strong> 2024-01-12 03:32:04</p>
    <h2>Code:</h2>
    <pre><code>CREATE PROCEDURE [Verkauf].[spAuftragStorno]
	@kAuftrag INT,
	@kBenutzer INT,
	@nValidate BIT,
	@kAuftragStornogrund INT,
	@cKommentar NVARCHAR(100) = NULL,
	@returnValue INT OUTPUT,
	@ignoreIstExterneRechnung BIT = 0,
	@ignorePendingStatus BIT = 0
AS
SET NOCOUNT ON;
SET ANSI_NULLS ON;
SET ANSI_NULL_DFLT_ON ON;
SET ANSI_PADDING ON;
SET CONCAT_NULL_YIELDS_NULL ON;
SET XACT_ABORT OFF;
DECLARE @OldContextInfo VARBINARY(128);
DECLARE @retry INT;
DECLARE @CreatedTransaction INT;
DECLARE @ErrorExists BIT = 0;
SET @returnValue = -1;
SET @retry = 5;
WHILE @retry &gt; 0
BEGIN
	IF(@nValidate = 1)
	BEGIN
		SET @returnValue = Verkauf.ifStorno(@kAuftrag, @ignoreIstExterneRechnung, @ignorePendingStatus);
		IF(@returnValue &gt; 0)
			RETURN;
	END
	IF NOT EXISTS(SELECT * FROM Verkauf.tAuftrag WHERE tAuftrag.kAuftrag = @kAuftrag)
	BEGIN
		RETURN;
	END

	DECLARE @amazonGrund INT;

	SELECT @amazonGrund = nAmazonMapping
	FROM Verkauf.tAuftragStornogrund
	WHERE kAuftragStornogrund = @kAuftragStornogrund

	IF(OBJECT_ID(&#39;tempdb..#StornoAmazon&#39;) IS NOT NULL)
	BEGIN
		DROP TABLE #StornoAmazon;
	END

	CREATE TABLE #StornoAmazon
	(
		kAmazonBestellung INT,
		cOrderId NVARCHAR(30)
	);

	IF EXISTS(
		SELECT *
		FROM Verkauf.tAuftrag
		JOIN tPlattform ON tPlattform.nPlattform = tAuftrag.kPlattform
		WHERE tAuftrag.kAuftrag = @kAuftrag AND tPlattform.nTyp = 6
	)
	BEGIN
		IF(@nValidate = 1 AND (@amazonGrund &lt; 0 OR @amazonGrund IS NULL))
		BEGIN
			SET @returnValue = 9;
			RETURN;
		END;
		INSERT INTO #StornoAmazon (kAmazonBestellung, cOrderId)
		SELECT DISTINCT pf_amazon_bestellung.kAmazonBestellung, pf_amazon_bestellung.cOrderId
		FROM Verkauf.tAuftragPosition
		JOIN pf_amazon_bestellungpos ON pf_amazon_bestellungpos.kAmazonBestellungPos = tAuftragPosition.kAmazonBestellungPos AND tAuftragPosition.kAmazonBestellungPos &gt; 0
		JOIN pf_amazon_bestellung ON pf_amazon_bestellung.kAmazonBestellung = pf_amazon_bestellungpos.kAmazonBestellung
		WHERE tAuftragPosition.kAuftrag = @kAuftrag AND pf_amazon_bestellung.kAmazonBestellung &gt; 0
	END;

	IF(CONTEXT_INFO() IS NOT NULL)
	BEGIN
		SET @OldContextInfo = CONTEXT_INFO();
	END
	ELSE
	BEGIN
		SET @OldContextInfo = 0x0;
	END
	DECLARE @hash VARBINARY(128);
	SELECT @hash = HASHBYTES(&#39;SHA1&#39;, &#39;Verkauf.spAuftragStorno&#39;);
	SET CONTEXT_INFO @hash; -- ContextInfo festlegen
	BEGIN TRY
		IF (@@TRANCOUNT = 0)
		BEGIN
			SET @CreatedTransaction = 1;
			BEGIN TRANSACTION
		END;
		ELSE
		BEGIN
			SET @CreatedTransaction = 0;
			SAVE TRANSACTION SpAuftragStorno;
		END;

		--Amazon Tabellen
		IF(@amazonGrund &gt;= 0)
		BEGIN
			UPDATE dbo.pf_amazon_bestellung
			SET nStatus = 5
			WHERE pf_amazon_bestellung.kAmazonBestellung IN(SELECT kAmazonBestellung FROM #StornoAmazon)

			MERGE INTO dbo.pf_amazon_bestellung_storno WITH (HOLDLOCK) AS target
			USING (SELECT #StornoAmazon.kAmazonBestellung, #StornoAmazon.cOrderId
				   FROM #StornoAmazon) AS source
			ON (target.cOrderId = source.cOrderId)
			WHEN MATCHED THEN
				UPDATE SET target.nCancelReason = @amazonGrund,
						   target.kBenutzer = @kBenutzer,
						   target.dBearbeitet = GETDATE()
			WHEN NOT MATCHED THEN
				INSERT (nCancelReason, kBenutzer, cOrderId, dErstellt) VALUES (@amazonGrund, @kBenutzer, source.cOrderId, GETDATE());

		END;
		
		INSERT INTO Verkauf.tAuftragStorno
		(
			kAuftrag,
			kBenutzer,
			kAuftragStornogrund,
			dStorniert,
			cKommentar
		)
		SELECT @kAuftrag, @kBenutzer, @kAuftragStornogrund, GETDATE(), @cKommentar;

		--Verkauf Tabellen
		UPDATE Verkauf.tAuftrag SET tAuftrag.nStorno = 1 WHERE tAuftrag.kAuftrag = @kAuftrag;

		--SCX
		UPDATE SCX.tOrder 
		SET tOrder.nCancellationUpload = 1 
		FROM SCX.tOrder
		JOIN Verkauf.tAuftrag ON tAuftrag.kAuftrag = tOrder.kAuftrag
		JOIN dbo.tShop ON tShop.kShop = tAuftrag.kShop
		WHERE tOrder.kAuftrag = @kAuftrag
			AND tShop.nAktiv = 1
			AND tShop.nGesperrt = 0;

		--Historie schreiben
		INSERT INTO [Kunde].[tHistorie]
		   ([kKunde]
		   ,[kAuftrag]
		   ,[kVorgang]
		   ,[kKey]
		   ,[cValue1]
		   ,[cValue2]
		   ,[cValue3]
		   ,[dErstellt]
		   ,[kBenutzer]
		   ,[fValue1]
		   ,[fValue2])
		SELECT
		   tAuftrag.kKunde,
		   @kAuftrag,
		   29,
		   @kAuftrag,
		   tAuftrag.cAuftragsNr,
		   NULL,
		   NULL,
		   GETDATE(),
		   @kBenutzer,
		   NULL,
		   NULL
		FROM Verkauf.tAuftrag
		WHERE tAuftrag.kAuftrag = @kAuftrag AND tAuftrag.kKunde IS NOT NULL;

		--
		-- tQueue schreiben
		--

		-- Connector-Bestellungen

		-- Bisherige Queue-Eintr&#228;ge entfernen
		DELETE dbo.tQueue
		FROM dbo.tQueue
		JOIN dbo.tShop ON tQueue.kShop = tShop.kShop
		WHERE tQueue.cName = &#39;tBestellung&#39; AND tShop.nTyp = 1 AND tQueue.kOption1 = @kAuftrag;

		-- Storno-Eintrag in tQueue schreiben
		INSERT INTO dbo.tQueue (kShop, kPlattform, cName, kWert, nAction, kOption1, kOption2, nInBearbeitung)
		SELECT
			tAuftrag.kShop,
			tAuftrag.kPlattform,
			&#39;tBestellung&#39;,
			0,
			5,
			tAuftrag.kAuftrag,
			0,
			0
		FROM Verkauf.tAuftrag
		JOIN dbo.tShop ON tAuftrag.kShop = tShop.kShop
		WHERE tAuftrag.kAuftrag = @kAuftrag AND tShop.nTyp = 1;

		-- JTL-Shop-Bestellungen

		-- Bisherige Queue-Eintr&#228;ge entfernen
		DELETE dbo.tQueue
		FROM dbo.tQueue
		JOIN Verkauf.tAuftrag ON (tQueue.kWert = tAuftrag.kShopauftrag AND tQueue.nAction IN(2, 5, 6))
			OR (tQueue.kWert = tAuftrag.kAuftrag AND tQueue.nAction IN(1))
		JOIN dbo.tShop ON tQueue.kShop = tShop.kShop
		WHERE dbo.tQueue.cName = &#39;tBestellung&#39; AND tShop.nTyp = 0 AND tAuftrag.kAuftrag = @kAuftrag;

		-- Storno-Eintrag in tQueue schreiben
		INSERT INTO dbo.tQueue (kShop, kPlattform, cName, kWert, nAction, kOption1, kOption2, nInBearbeitung)
		SELECT
			tAuftrag.kShop,
			tAuftrag.kPlattform,
			&#39;tBestellung&#39;,
			tAuftrag.kShopauftrag,
			5,
			0,
			0,
			0
		FROM Verkauf.tAuftrag
		JOIN dbo.tShop ON tAuftrag.kShop = tShop.kShop
		WHERE tAuftrag.kAuftrag = @kAuftrag AND tShop.nTyp = 0;

		--tLog bei extern abgerechneten Auftrag schreiben
		INSERT INTO dbo.tLog(dDatum, kBenutzer, cLog, nTyp, nVorgang)
		SELECT 
			GETDATE() AS dDatum, 
			@kBenutzer AS kBenutzer, 
			&#39;Auftrag &#39; + tAuftrag.cAuftragsNr + &#39; (&#39; + CONVERT(NVARCHAR, tAuftrag.kAuftrag) + &#39;) wurde trotz externer Abrechnung storniert!&#39; AS cLog, 
			2 AS nTyp, --Auftrag
			2 AS nVorgang --Bearbeiten
		FROM Verkauf.tAuftrag
		WHERE tAuftrag.kAuftrag = @kAuftrag AND tAuftrag.nIstExterneRechnung &gt; 0

		--EckdatenBerechnen
		DECLARE @Eckdaten AS Verkauf.TYPE_spAuftragEckdatenBerechnen;
		INSERT INTO @Eckdaten ( kAuftrag ) VALUES (@kAuftrag);
		EXEC Verkauf.spAuftragEckdatenBerechnen @Eckdaten;

		IF(@CreatedTransaction = 1)
		BEGIN
			COMMIT-- Nur wenn kein Savepoint gesetzt
		END
		SET @retry = -1;
		SET @returnValue = 0;
	END TRY
	BEGIN CATCH
		-- Deadlock Catch
		IF(ERROR_NUMBER() = 1205)
		BEGIN
			SET @retry = @retry - 1;
			IF(@CreatedTransaction = 1)
			BEGIN
			    ROLLBACK TRANSACTION;
			END
			ELSE
			BEGIN
				ROLLBACK TRANSACTION SpAuftragStorno;
			END
			IF(@retry = 0)
			BEGIN
				SET CONTEXT_INFO 0x0;
				THROW;
				RETURN;
			END
			EXEC dbo.spWaitRandom;
		END
		ELSE
		BEGIN
			SET @retry = - 1;
			IF(@CreatedTransaction = 1)
			BEGIN
				ROLLBACK TRANSACTION;
			END
			ELSE
			BEGIN
			    ROLLBACK TRANSACTION SpAuftragStorno;
			END
			SET CONTEXT_INFO @OldContextInfo;
			THROW
			RETURN;
		END
	END CATCH
END</code></pre>
</body>
</html>
