<!DOCTYPE html>
<html lang="en">
<head>
    <meta charset="UTF-8">
    <meta name="viewport" content="width=device-width, initial-scale=1.0">
    <title>Auslieferung.spReserviereBestellpositionen</title>
    <style>
        body { font-family: Arial, sans-serif; line-height: 1.6; margin: 0; padding: 20px; }
        h1 { color: #333; }
        pre { background-color: #f4f4f4; padding: 10px; border-radius: 5px; overflow-x: auto; }
    </style>
</head>
<body>
    <h1>Auslieferung.spReserviereBestellpositionen</h1>
    <p><strong>Description:</strong> Keine Beschreibung hinterlegt.</p>
    <p><strong>Long Description:</strong> Keine ausf√ºhrliche Beschreibung hinterlegt.</p>
    <p><strong>Created At:</strong> 2024-01-12 03:32:03</p>
    <p><strong>Updated At:</strong> 2024-01-12 03:32:03</p>
    <h2>Code:</h2>
    <pre><code>CREATE PROCEDURE [Auslieferung].[spReserviereBestellpositionen]  

-- Reservierung mehrerer Bestellpositionen  
--
-- Transaction-Handling: siehe https://msdn.microsoft.com/en-us/library/ms188378.aspx
--

@Bestellpositionen XML,
-- &lt;Bestellposition&gt;
--   &lt;kBestellPos&gt;1&lt;/kBestellPos&gt;
--   &lt;fAnzahl&gt;1.234&lt;/fAnzahl&gt;
-- &lt;/Bestellposition&gt;
-- &lt;Bestellposition&gt;
--   &lt;kBestellPos&gt;1&lt;/kBestellPos&gt;
--   &lt;fAnzahl&gt;1.234&lt;/fAnzahl&gt;
-- &lt;/Bestellposition&gt;

@Laeger XML,
-- &lt;Warenlagereingang&gt;
--   &lt;kWarenlagereingang&gt;17&lt;/kWarenlagereingang&gt;
--   &lt;fAnzahl&gt;2.0&lt;/fAnzahl&gt;
--   &lt;nPrio&gt;0&lt;/nPrio&gt;
-- &lt;/Warenlagereingang&gt;
-- &lt;Warenlagereingang&gt;
--   &lt;kWarenlagereingang&gt;18&lt;/kWarenlagereingang&gt;
--   &lt;fAnzahl&gt;2.0&lt;/fAnzahl&gt;
--   &lt;nPrio&gt;1&lt;/nPrio&gt;
-- &lt;/Warenlagereingang&gt;
@Warenlagereingaenge XML,

-- Optionen f&#252;r die Auslieferung (Bitmaske)
-- 0x0001 = Pro Bestellung eine Pickliste anlegen
-- 0x0100 = St&#252;cklistenkorrektur vornehmen
@nOptions INT,

@kBenutzer INT,

-- SitzungsId
@kSessionId INT

AS  
SET NOCOUNT ON;
SET ANSI_NULLS ON;
SET ANSI_NULL_DFLT_ON ON;
SET ANSI_PADDING ON;
SET CONCAT_NULL_YIELDS_NULL ON;
SET XACT_ABORT OFF;


DECLARE @TranCounter INT;
SET @TranCounter = @@TRANCOUNT;
IF @TranCounter &gt; 0
	SAVE TRANSACTION spReserviereBestellpositionen
ELSE
	BEGIN TRANSACTION;

BEGIN TRY
    DECLARE @kBestellPos AS INT;
    DECLARE @fAnzahl AS DECIMAL(28,14);

    DECLARE cBestellpos CURSOR LOCAL FAST_FORWARD FOR
        SELECT 
           ParamValues.ID.value(&#39;kBestellPos[1]&#39;, &#39;INT&#39;),      
           ParamValues.ID.value(&#39;fAnzahl[1]&#39;, &#39;DECIMAL(25,13)&#39;)
        FROM @Bestellpositionen.nodes(&#39;/Bestellposition&#39;) AS ParamValues(ID);
	OPEN cBestellpos;
	FETCH NEXT FROM cBestellpos INTO @kBestellPos, @fAnzahl;
	WHILE @@FETCH_STATUS = 0
	BEGIN
		EXEC Auslieferung.spReserviereBestellposition
			@kBestellPos = @kBestellPos,
			@fAnzahl = @fAnzahl,
			@nOptions = @nOptions,
			@Laeger = @Laeger,
			@Warenlagereingaenge = @Warenlagereingaenge,
			@kBenutzer = @kBenutzer,
			@kSessionId = @kSessionId;
		FETCH NEXT FROM cBestellpos INTO @kBestellPos, @fAnzahl;
	END
	CLOSE cBestellpos;
	DEALLOCATE cBestellpos;

	IF ((@nOptions &amp; 0x0100) &gt; 0)
	BEGIN
     	EXEC [Auslieferung].[spPicklistenStuecklistenFix]
		  @kBenutzer = @kBenutzer,
		  @kSessionId = @kSessionId
     END
	IF @TranCounter = 0
		COMMIT TRANSACTION;
END TRY
BEGIN CATCH
	IF @TranCounter = 0
		ROLLBACK TRANSACTION;
	ELSE
		IF XACT_STATE() &lt;&gt; -1
			ROLLBACK TRANSACTION spReserviereBestellpositionen;

	DECLARE @ErrorMessage NVARCHAR(4000);
    DECLARE @ErrorSeverity INT;
    DECLARE @ErrorState INT;

    SELECT @ErrorMessage = ERROR_MESSAGE();
    SELECT @ErrorSeverity = ERROR_SEVERITY();
    SELECT @ErrorState = ERROR_STATE();

    RAISERROR (@ErrorMessage, -- Message text.
               @ErrorSeverity, -- Severity.
               @ErrorState -- State.
              );
END CATCH</code></pre>
</body>
</html>
